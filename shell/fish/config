#!/bin/fish

set fish_greeting

function __fish_preexec_history --on-event fish_preexec
  if not test -d $XDG_DATA_HOME/fish/history
    mkdir -p $XDG_DATA_HOME/fish/history
  end
  echo "["(date)"]" $fish_pid $USER $PWD";" $argv >> "$XDG_DATA_HOME/fish/history/history-"(date +%Y%m%d)
end

function zellij_tab_name_update --on-event fish_postexec
    if set -q ZELLIJ
        set title (string replace -r "^/Users/roxton" "~" $PWD)
        command nohup zellij action rename-tab "$title" >/dev/null 2>&1
    end
end

function fish_title
    echo (pwd | awk '{ sub("/Users/nick.wolf", "~"); print $0 }')
end

for file in $XDG_DATA_HOME/environment/*
    source $file
end

for file in $XDG_DATA_HOME/fish/environment/*
    source $file
end

if test -s $XDG_DATA_HOME/godspeed-cli/cache
    echo "WARNING: Godspeed-cli has unsynchronized changes"
end

function git_check
  git -C $argv[1] rev-parse 2>/dev/null
  if test $status -eq 128
    echo "$argv[1] does not exist or is not a git repository"
    return 128
  end
  set starting_directory (pwd)
  cd $argv[1]
  git fetch --quiet >/dev/null 2>&1 & disown
  git diff --quiet --ignore-submodules HEAD
  if test $status -eq 1
    echo "WARNING: $argv[2] has unsynchronized changes"

    # Check if auto-commit is enabled (3rd argument passed)
    if test -n "$argv[3]"
      # Get the timestamp of the last commit
      set last_commit_time (git log -1 --format=%ct)
      set current_time (date +%s)
      set time_diff (math $current_time - $last_commit_time)
      set hours_diff (math $time_diff / 3600)

      if test $hours_diff -gt 24
        echo "Last commit was $hours_diff hours ago. Auto-committing changes..."
        git add -A
        git commit -m "Auto-commit: unsynchronized changes from $(date '+%Y-%m-%d %H:%M:%S')"
        echo "Changes committed successfully"
      end
    end
  end
  cd $starting_directory
  set -e starting_directory
end

git_check $HOME/.dotfiles 'Dotfiles'
git_check $HOME/Documents/obsidian 'Obsidian' "auto"
git_check $HOME/.config/alfred/Alfred.alfredpreferences 'Alfred'

mise activate fish | source

starship init fish | source
zoxide init fish --cmd f | source
